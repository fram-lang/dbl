{# This file is part of DBL, released under MIT license.
 # See LICENSE for details.
 #}

import open /Base/Types
import open /Base/Assert
import open /Base/String
import open /Base/Operators

{## For `Some x` returns `x`, otherwise the provided argument is returned. ##}
pub method unwrapOr self default =
  match self with
  | None   => default
  | Some x => x
  end

{## For `Some x` returns `x`, otherwise the function `~onError` is called and
  its result returned. ##}
pub method unwrapErr {~onError} self =
  match self with
  | None   => ~onError ()
  | Some x => x
  end

{## This method should only be called on `Some x` values, in which case it
  returns `x`. When applied to `None` the entire program crashes irrecoverably
  with a runtime error. The resulting error message can be optionally specified
  with `?msg`. ##}
pub method unwrap {~__line__, ~__file__, ?msg} self =
  match self with
  | None   => runtimeError (msg.unwrapOr "Called `unwrap` on `None`")
  | Some x => x
  end

{## Maps value stored in `Some`. ##}
pub method map self f =
  match self with
  | None   => None
  | Some x => Some (f x)
  end

{##
  Checks whether two optional values are equal.
  Returns true if both are empty or if both contains equal values;
  returns false otherwise.
 ##}
pub method equal {X, method equal : X -> X ->[] Bool} (o1 : Option X) o2 =
  match (o1, o2) with
  | (Some x1, Some x2) => x1 == x2
  | (None,    None   ) => True
  | (_,       _      ) => False
  end
