import AnsiTerminal as Term

pub data Color =
  | Black
  | Red
  | Green
  | Yellow
  | Blue
  | Magenta
  | Cyan
  | White

method toAnsi (color : Color) =
  match color with
  | Black   => Term.Font.Black
  | Red     => Term.Font.Red
  | Green   => Term.Font.Green
  | Yellow  => Term.Font.Yellow
  | Blue    => Term.Font.Blue
  | Magenta => Term.Font.Magenta
  | Cyan    => Term.Font.Cyan
  | White   => Term.Font.White
  end

pub data Style =
  | Clear
  | Style of { ?fg : Color
             , ?bg : Color
             , ?underline : Bool
             , ?strike : Bool
             }

method getCode (self : Style) = match self with
  | Clear => Term.Font.resetAll
  | Style {fg, bg, underline, strike} =>
      match fg with
      | Some c => Term.Font.setFgColor c.toAnsi
      | None => ""
      end
    + match bg with
      | Some c => Term.Font.setBgColor c.toAnsi
      | None => ""
      end
    + match underline with
      | Some True => Term.Font.setUnderline
      | Some False => Term.Font.resetUnderline
      | None => ""
      end
    + match strike with
      | Some True => Term.Font.setStrikeout
      | Some False => Term.Font.resetStrikeout
      | None => ""
      end
  end

# produces next ansi string to continue style
pub data Stylist E =
  { pushStyle : Style ->[E] String
  , popStyle  : Unit  ->[E] String
  }

pub let styleH =
  handler
    Stylist
      { pushStyle = effect s / r => fn st => r s.getCode (s :: st)
      , popStyle  = effect s / r => fn st => match st with
          | _ :: y :: xs => r y.getCode (y :: xs)
          | _ => impossible ()
          end
      }
    return x => fn _ => x
    finally c => c [Clear]
  end
