{# This file is part of DBL, released under the MIT license.
 # See LICENSE for details.
 #}

## # String operations 

{##
  Returns a substring of length `len` starting at index `pos`. Returns `None`
  when requested range is no contained within string.
 ##}
pub let substring (str : String) (pos : Int) (len : Int) =
  if (pos < 0 || len < 0 || pos + len > str.length) then
    None
  else
   (let rec iter i acc =
      if i < pos
        then acc
        else iter (i - 1) (str.get i :: acc)
    in
    Some (charListToStr (iter (pos + len - 1) [])))

{##
  Returns a index of first occurence of given char and `None` if
  such position was not found.

  @param ?beg Initial search position. Allows to iterativately
    find all occurences of given char.
 ##}
pub let findChar {?beg} (str : String) (chr : Char) =
  let beg = beg.unwrapOr 0
  let rec iter i =
    if i >= str.length then
      None
    else if str.get i == chr then
      Some i
    else
      iter (i + 1)
  in
  iter beg 

{##
  Returns a list of strings aquired by splitting the string on
  a delimiting character.
 ##}
pub let splitOn (str : String) (delim : Char) =
  let rec iter i e acc =
    if i <= -1 then
      substring str (i + 1) e >.unwrap :: acc
    else if str.get i == delim then
      iter (i - 1) i (substring str (i + 1) (e - i - 1) >.unwrap :: acc)
    else
      iter (i - 1) e acc
  in
  iter (str.length - 1) str.length ([] : List String)

pub method findChar = findChar
pub method substring = substring
pub method splitOn = splitOn
