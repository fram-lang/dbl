{# 
This file is part of DBL, released under MIT license.
See LICENSE for details.
#}

import open Mutable
import open /Base/Bool

data Vector E T = Vector of
  { content   : Ref E (Array E (Option T))
  , size      : Ref E Int
  , capacity  : Ref E Int
  , mut       : Mut E
  }

parameter E
parameter T

# Helper methods for vector implementation.
method getContentAt (Vector{ content } : Vector E T) (n : Int) =
  content.get.get n

method setContent (Vector{ content } : Vector E T) new_content =
  content := new_content

method setContentAt (Vector{ content } : Vector E T) (n : Int) (x : T) =
  content.get.at n := (Some x)

method mut (Vector{ mut } : Vector E T) = mut

let getSmallestUpperPow2 (n : Int) =
  let n = n-1 in
  let n = n ||| (n>>1) in
  let n = n ||| (n>>2) in
  let n = n ||| (n>>4) in
  let n = n ||| (n>>8) in
  let n = n ||| (n>>16) in
  let n = n ||| (n>>32) in
  let n = n ||| (n>>64) in
  n+1

pub let makeVector {T} (size : Int) =
  let capacity = (getSmallestUpperPow2 size) in
  Vector
  { content    = ioMut.ref (ioMut.makeArray capacity (None : Option T))
  , size       = ioMut.ref size
  , capacity   = ioMut.ref capacity
  , mut        = ioMut
  }

pub method capacity (Vector{ capacity } : Vector E T) = capacity.get

method setCapacity (Vector{ capacity } : Vector E T) (n : Int) =
  capacity := n

pub method size (Vector{ size } : Vector E T) = size.get

method setSize (Vector{ size } : Vector E T) (n : Int) =
  size := n 

pub let maxVectorSize = maxArrayLength

# Returns true if the vector is empty
pub method empty (Vector{ size } : Vector E T) = size.get == 0

{# 
Resizes the vector to contain n elements.
If n is smaller than the current size, the vector is truncated.
If n is greater than size, the vector is expanded.
If n exceeds the current capacity, the vector reallocates its storage.
#}
pub method resize (self : Vector E T) (n : Int) =
  self.setSize n;
  let new_capacity = getSmallestUpperPow2 n in
  if new_capacity <= self.capacity then ()
  else (
    let new_content = self.mut.initArray new_capacity (fn i =>
      if i < self.capacity then self.getContentAt i else None) in
    self.setContent new_content;
    self.setCapacity new_capacity
  )

# Get the nth element of a vector.
pub method get (self : Vector E T) (n : Int) =
  assert {msg="Vector is empty"} (self.empty.neg);
  assert {msg="Index out of range"} (n >= 0 && n < self.size);
  self.getContentAt n

# Set the nth element of a vector.
pub method set (self : Vector E T) (n : Int) (x : T) =
  assert {msg="Vector is empty"} (self.empty.neg);
  assert {msg="Index out of range"} (n >= 0 && n < self.size);
  self.setContentAt n x

pub method front (self : Vector E T) = self.get 0

pub method back (self : Vector E T) = self.get (self.size - 1)

pub method pushBack (self : Vector E T) (x : T) =
  let new_size = self.size + 1 in
  if new_size >= self.capacity then
    self.resize new_size
  else
    self.setSize new_size;
  self.set (new_size - 1) x

pub method popBack (self : Vector E T) =
  assert {msg="Vector is empty"} (self.empty.neg);
  self.setSize (self.size - 1)

pub method clear (self : Vector E T) =
  self.setSize 0;
  self.setCapacity 0;
  self.setContent (self.mut.makeArray 0 (None : Option T))
  